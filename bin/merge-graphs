#!/run/current-system/profile/bin/guile \
-e main -s
!#

(use-modules (config)
             (config api)
             (config parser sexp)
             (guix build utils)
             (ice-9 format)
             (ice-9 match)
             (ice-9 pretty-print)
             (ice-9 rdelim)
             (ice-9 threads)
             (rdf rdf)
             (turtle fromrdf)
             (turtle tordf)
             (srfi srfi-1))

(define config
  (configuration
   (name 'merge-graphs)
   (keywords
    (list
     (switch
      (name 'ids) (handler canonicalize-path) (optional? #f)
      (test file-exists?) (example "/data/ids.tsv")
      (synopsis "Tweet ids in TSV format."))
     (switch
      (name 'dbpedia) (handler canonicalize-path) (optional? #f)
      (test file-exists?) (example "/data/dbpedia")
      (synopsis "Path to Dbpedia feature graphs."))
     (switch
      (name 'tweetskb) (handler canonicalize-path) (optional? #f)
      (test file-exists?) (example "/data/tweetskb")
      (synopsis "Path to TweetsKB feature graphs."))
     (switch
      (name 'uby) (handler canonicalize-path) (optional? #f)
      (test file-exists?) (example "/data/uby")
      (synopsis "Path to UBY feature graphs."))
     (switch
      (name 'output) (handler identity) (optional? #f) (example "/data/out")
      (synopsis "Path to the directory where the merged graphs will be exported."))))
   (synopsis "Merge Turle graphs")
   (description "merge-graphs is a script that merges different Turtle graphs
into a single output graph.")
   (parser sexp-parser)))

(define* (line-map port #:optional (proc (lambda (x) x)))
  (let loop ((acc '()))
    (match (read-line port)
      ((? eof-object?) acc)
      (line
       (let ((res (proc line)))
         (loop (cons res acc)))))))

(define (main args)
  (let* ((options (getopt-config-auto (command-line) config))
         (out-dir (option-ref options 'output))
         (feature-graphs-paths (list
                                (option-ref options 'dbpedia)
                                (option-ref options 'tweetskb)
                                (option-ref options 'uby)))
         (ids
          (call-with-input-file (option-ref options 'ids)
            (lambda (port)
              (line-map port string-trim-both)))))
    (mkdir-p out-dir)
    (n-par-map (1- (* (current-processor-count) 2))
               (lambda (id)
                 (let ((out-path (string-append out-dir
                                                "/" id ".ttl"))
                       (graphs
                        (filter (compose not null?)
                                (map (lambda (data-path)
                                       (turtle->rdf data-path
                                                    "https://www.lemon-model.net/lexica/uby/vn/"))
                                     (filter file-exists?
                                             (map (lambda (data-dir)
                                                    (string-append data-dir "/" id ".ttl"))
                                                  feature-graphs-paths))))))
                   (format #t "Exporting bag to ~a...\n" out-path)
                   (with-output-to-file out-path
                     (lambda _
                       (display
                        (rdf->turtle
                         (if (>= (length graphs) 2)
                             (fold merge-graphs
                                   (car graphs)
                                   (cdr graphs))
                             (car graphs))))))))
               ids)))
